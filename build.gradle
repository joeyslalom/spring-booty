buildscript {
	ext {
		springBootVersion = '1.5.2.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath 'se.transmode.gradle:gradle-docker:1.2'
		classpath "net.ltgt.gradle:gradle-apt-plugin:0.10"
	}
}

apply plugin: 'java'
apply plugin: "net.ltgt.apt"
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "io.springfox:springfox-swagger2:2.6.1"
	compile "io.springfox:springfox-swagger-ui:2.6.1"

	compileOnly "com.google.auto.value:auto-value:1.2"
	apt "com.google.auto.value:auto-value:1.2"

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

task buildDocker(type: Docker, dependsOn: build) {
	baseImage = 'openjdk:8'
	applicationName = "spring-booty"
	tagVersion = "latest"
	addFile {
		from jar
		rename {'app.jar'}
	}
	entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
	exposePort(8080)
}