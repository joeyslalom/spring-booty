buildscript {
	ext {
		springBootVersion = '1.5.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath 'se.transmode.gradle:gradle-docker:1.2'
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'


compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.warnings = true
	options.deprecation = true
	options.compilerArgs += ["-Xlint:all", "-Werror"]
}

jar {
	baseName = "spring-booty"
	version =  '0.0.1-SNAPSHOT'
}

repositories {
	mavenCentral()
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "org.springframework.boot:spring-boot-starter-actuator"

	compile "io.springfox:springfox-swagger2:2.6.1"
	compile "io.springfox:springfox-swagger-ui:2.6.1"

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

task buildDocker(type: Docker, dependsOn: build) {
	baseImage = 'openjdk:8'
	applicationName = jar.baseName
	tagVersion = "latest"
	addFile {
		from jar
		rename {'app.jar'}
	}
	entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
	exposePort(8080)
}